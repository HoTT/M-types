{-# OPTIONS --without-K #-}
module container.m.from-nat.bisimulation where

open import level
open import sum
open import equality
open import function
open import container.core
open import container.m.coalgebra as MC hiding (IsMor ; _â‡’_)
open import container.m.from-nat.coalgebra hiding (X)
open import hott.level

module Def {la lb lc} {C : Container la lb lc} (ğ“§ : Coalg C (lb âŠ” lc)) where

  open Container C
  open Î£ ğ“§ renaming (projâ‚ to X ; projâ‚‚ to Î³)
  open MC C using (IsMor ; _â‡’_)

  -- Î£-closure of an indexed binary relation
  Î£â‚‚[_] : (âˆ€ {i} â†’ X i â†’ X i â†’ Set (lb âŠ” lc)) â†’ I â†’ Set _
  Î£â‚‚[ _âˆ¼_ ] i = Î£ (X i) Î» x â†’ Î£ (X i) Î» xâ€² â†’ x âˆ¼ xâ€²

  -- projections
  module _ {_âˆ¼_ : âˆ€ {i} â†’ X i â†’ X i â†’ Set _} (i : I) where

    Î£â‚‚-projâ‚ : Î£â‚‚[ _âˆ¼_ ] i â†’ X i
    Î£â‚‚-projâ‚ = projâ‚

    Î£â‚‚-projâ‚‚ : Î£â‚‚[ _âˆ¼_ ] i â†’ X i
    Î£â‚‚-projâ‚‚ = projâ‚ âˆ˜' projâ‚‚

    Î£â‚‚-projâ‚ƒ : (r : Î£â‚‚[ _âˆ¼_ ] i) â†’ _âˆ¼_ (Î£â‚‚-projâ‚ r) (Î£â‚‚-projâ‚‚ r)
    Î£â‚‚-projâ‚ƒ = projâ‚‚ âˆ˜' projâ‚‚

  -- Definition 16 in Ahrens, Capriotti and Spadotti (arXiv:1504.02949v1 [cs.LO])
  -- bisimulation definition
  record Bisim (_âˆ¼_ : âˆ€ {i} â†’ X i â†’ X i â†’ Set _): Set(lb âŠ” lc âŠ” lsuc la) where
    field
      Î± : Î£â‚‚[ _âˆ¼_ ] â†’â± F Î£â‚‚[ _âˆ¼_ ]
      Ï€â‚-Mor : IsMor (_ , Î±) ğ“§ Î£â‚‚-projâ‚
      Ï€â‚‚-Mor : IsMor (_ , Î±) ğ“§ Î£â‚‚-projâ‚‚

    ğ“‘ : Coalg C _
    ğ“‘ = _ , Î±

    Ï€â‚ : ğ“‘ â‡’ ğ“§
    Ï€â‚ = _ , Ï€â‚-Mor

    Ï€â‚‚ : ğ“‘ â‡’ ğ“§
    Ï€â‚‚ = _ , Ï€â‚‚-Mor

  -- Lemma 17 in Ahrens, Capriotti and Spadotti (arXiv:1504.02949v1 [cs.LO])
  Î” : Bisim (Î» {i} â†’ _â‰¡_)
  Î” = record { Î± = Î± ; Ï€â‚-Mor = Ï€â‚-Mor ; Ï€â‚‚-Mor = Ï€â‚‚-Mor }
    where Î± : Î£â‚‚[ _â‰¡_ ] â†’â± F Î£â‚‚[ _â‰¡_ ]
          Î± i (x , ._ , refl) = projâ‚ (Î³ _ x)
                                  , Î» b â†’ (projâ‚‚ (Î³ _ x) b) , (_ , refl)
          Ï€â‚-Mor : IsMor (_ , Î±) ğ“§ _
          Ï€â‚-Mor = funextâ± helper
            where helper : (i : I) â†’ (p : Î£â‚‚[ _â‰¡_ ] i) â†’ _
                  helper i (m , ._ , refl) = refl
          Ï€â‚‚-Mor : IsMor (_ , Î±) ğ“§ _
          Ï€â‚‚-Mor = funextâ± helper
            where helper : (i : I) â†’ (p : Î£â‚‚[ _â‰¡_ ] i) â†’ _
                  helper i (m , ._ , refl) = refl


--------------------------------------------------------------------------------
-- coinduction proof principle

module _ {la lb lc} {C : Container la lb lc} where

  open Container C
  open MC C using (IsMor ; _â‡’_)

  private
    ğ“œ : Coalg C (lb âŠ” lc)
    ğ“œ = ğ“› C
    unfold : âˆ€ (ğ“§ : Coalg C (lb âŠ” lc)) â†’ ğ“§ â‡’ ğ“œ
    unfold ğ“§ = projâ‚ $ lim-terminal C ğ“§
    unfold-universal = Î» {â„“} (ğ“§ : Coalg C â„“) â†’ projâ‚‚ (lim-terminal C ğ“§)

  open Î£ ğ“œ renaming (projâ‚ to M ; projâ‚‚ to out) ; open Def ğ“œ

  module _ {_âˆ¼_ : âˆ€ {i} â†’ M i â†’ M i â†’ Set (lb âŠ” lc)} (B : Bisim _âˆ¼_) where

    -- Theorem 18 in Ahrens, Capriotti and Spadotti (arXiv:1504.02949v1 [cs.LO])
    -- coinduction proof principle
    cpp : âˆ€ {i} {m mâ€² : M i} â†’ m âˆ¼ mâ€² â†’ m â‰¡ mâ€²
    cpp {i} p = funext-invâ± (projâ‚ $ apÎ£ Ï€â‚=Ï€â‚‚) i (_ , _ , p)
      where open Bisim B
            abstract
              Ï€â‚=Ï€â‚‚ : Ï€â‚ â‰¡ Ï€â‚‚
              Ï€â‚=Ï€â‚‚ = (sym $ unfold-universal ğ“‘ Ï€â‚) Â· unfold-universal ğ“‘ Ï€â‚‚


    -- In particular, provided that the bisimulation _âˆ¼_ is reflexive, we have:
    module _ (âˆ¼-refl : âˆ€ {i} {m : M i} â†’ m âˆ¼ m) where

      cppâ€² : âˆ€ {i} {m mâ€² : M i} â†’ m âˆ¼ mâ€² â†’ m â‰¡ mâ€²
      cppâ€² {i} p = cpp p Â· sym (cpp âˆ¼-refl)

      cppâ€²-inv : âˆ€ {i} {m mâ€² : M i} â†’ m â‰¡ mâ€² â†’ m âˆ¼ mâ€²
      cppâ€²-inv refl = âˆ¼-refl

      cppâ€²-id : âˆ€ {i} {m : M i} â†’ cppâ€² âˆ¼-refl â‰¡ refl {x = m}
      cppâ€²-id = left-inverse $ cpp âˆ¼-refl

      cppâ€²-retraction : âˆ€ {i} {m mâ€² : M i} (p : m â‰¡ mâ€²) â†’ cppâ€² (cppâ€²-inv p) â‰¡ p
      cppâ€²-retraction refl = left-inverse $ cpp âˆ¼-refl
